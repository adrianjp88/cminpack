                Documentation for MINPACK subroutine LMDER
                         Double precision version
                       Argonne National Laboratory
          Burton S. Garbow, Kenneth E. Hillstrom, Jorge J. More
                                March 1980
 
  1. Purpose.
        The purpose of LMDER is to minimize the sum of the squares of M
        nonlinear functions in N variables by a modification of the
        Levenberg-Marquardt algorithm.  The user must provide a subrou-
        tine which calculates the functions and the Jacobian.
 
  2. Subroutine and type statements.
        SUBROUTINE LMDER(FCN,M,N,X,FVEC,FJAC,LDFJAC,FTOL,XTOL,GTOL,
       *                 MAXFEV,DIAG,MODE,FACTOR,NPRINT,INFO,NFEV,NJEV,
       *                 IPVT,QTF,WA1,WA2,WA3,WA4)
        INTEGER M,N,LDFJAC,MAXFEV,MODE,NPRINT,INFO,NFEV,NJEV
        INTEGER IPVT(N)
        DOUBLE PRECISION FTOL,XTOL,GTOL,FACTOR
        DOUBLE PRECISION X(N),FVEC(M),FJAC(LDFJAC,N),DIAG(N),QTF(N),
       *                 WA1(N),WA2(N),WA3(N),WA4(M)
 
  3. Parameters.
        Parameters designated as input parameters must be specified on
        entry to LMDER and are not changed on exit, while parameters
        designated as output parameters need not be specified on entry
        and are set to appropriate values on exit from LMDER.
        FCN is the name of the user-supplied subroutine which calculate
          the functions and the Jacobian.  FCN must be declared in an
          EXTERNAL statement in the user calling program, and should be
          written as follows.
          SUBROUTINE FCN(M,N,X,FVEC,FJAC,LDFJAC,IFLAG)
          INTEGER M,N,LDFJAC,IFLAG
          DOUBLE PRECISION X(N),FVEC(M),FJAC(LDFJAC,N)
          ----------
          IF IFLAG = 1 CALCULATE THE FUNCTIONS AT X AND
          RETURN THIS VECTOR IN FVEC.  DO NOT ALTER FJAC.
          IF IFLAG = 2 CALCULATE THE JACOBIAN AT X AND
          RETURN THIS MATRIX IN FJAC.  DO NOT ALTER FVEC.
          ----------
          RETURN
          END
 
                                                                  Page
          The value of IFLAG should not be changed by FCN unless the
          user wants to terminate execution of LMDER.  In this case set
          IFLAG to a negative integer.
        M is a positive integer input variable set to the number of
          functions.
        N is a positive integer input variable set to the number of
          variables.  N must not exceed M.
        X is an array of length N.  On input X must contain an initial
          estimate of the solution vector.  On output X contains the
          final estimate of the solution vector.
        FVEC is an output array of length M which contains the function
          evaluated at the output X.
        FJAC is an output M by N array.  The upper N by N submatrix of
          FJAC contains an upper triangular matrix R with diagonal ele-
          ments of nonincreasing magnitude such that
                 T     T           T
                P *(JAC *JAC)*P = R *R,
          where P is a permutation matrix and JAC is the final calcu-
          lated Jacobian.  Column j of P is column IPVT(j) (see below)
          of the identity matrix.  The lower trapezoidal part of FJAC
          contains information generated during the computation of R.
        LDFJAC is a positive integer input variable not less than M
          which specifies the leading dimension of the array FJAC.
        FTOL is a nonnegative input variable.  Termination occurs when
          both the actual and predicted relative reductions in the sum
          of squares are at most FTOL.  Therefore, FTOL measures the
          relative error desired in the sum of squares.  Section 4 con-
          tains more details about FTOL.
        XTOL is a nonnegative input variable.  Termination occurs when
          the relative error between two consecutive iterates is at most
          XTOL.  Therefore, XTOL measures the relative error desired in
          the approximate solution.  Section 4 contains more details
          about XTOL.
        GTOL is a nonnegative input variable.  Termination occurs when
          the cosine of the angle between FVEC and any column of the
          Jacobian is at most GTOL in absolute value.  Therefore, GTOL
          measures the orthogonality desired between the function vector
          and the columns of the Jacobian.  Section 4 contains more
          details about GTOL.
        MAXFEV is a positive integer input variable.  Termination occur
          when the number of calls to FCN with IFLAG = 1 has reached
          MAXFEV.
 
                                                                  Page
        DIAG is an array of length N.  If MODE = 1 (see below), DIAG is
          internally set.  If MODE = 2, DIAG must contain positive
          entries that serve as multiplicative scale factors for the
          variables.
        MODE is an integer input variable.  If MODE = 1, the variables
          will be scaled internally.  If MODE = 2, the scaling is speci-
          fied by the input DIAG.  Other values of MODE are equivalent
          to MODE = 1.
        FACTOR is a positive input variable used in determining the ini-
          tial step bound.  This bound is set to the product of FACTOR
          and the Euclidean norm of DIAG*X if nonzero, or else to FACTO
          itself.  In most cases FACTOR should lie in the interval
          (.1,100.).  100. is a generally recommended value.
        NPRINT is an integer input variable that enables controlled
          printing of iterates if it is positive.  In this case, FCN is
          called with IFLAG = 0 at the beginning of the first iteration
          and every NPRINT iterations thereafter and immediately prior
          to return, with X, FVEC, and FJAC available for printing.
          FVEC and FJAC should not be altered.  If NPRINT is not posi-
          tive, no special calls of FCN with IFLAG = 0 are made.
        INFO is an integer output variable.  If the user has terminated
          execution, INFO is set to the (negative) value of IFLAG.  See
          description of FCN.  Otherwise, INFO is set as follows.
          INFO = 0  Improper input parameters.
          INFO = 1  Both actual and predicted relative reductions in th
                    sum of squares are at most FTOL.
          INFO = 2  Relative error between two consecutive iterates is
                    at most XTOL.
          INFO = 3  Conditions for INFO = 1 and INFO = 2 both hold.
          INFO = 4  The cosine of the angle between FVEC and any column
                    of the Jacobian is at most GTOL in absolute value.
          INFO = 5  Number of calls to FCN with IFLAG = 1 has reached
                    MAXFEV.
          INFO = 6  FTOL is too small.  No further reduction in the sum
                    of squares is possible.
          INFO = 7  XTOL is too small.  No further improvement in the
                    approximate solution X is possible.
          INFO = 8  GTOL is too small.  FVEC is orthogonal to the
                    columns of the Jacobian to machine precision.
          Sections 4 and 5 contain more details about INFO.
 
                                                                  Page
        NFEV is an integer output variable set to the number of calls t
          FCN with IFLAG = 1.
        NJEV is an integer output variable set to the number of calls t
          FCN with IFLAG = 2.
        IPVT is an integer output array of length N.  IPVT defines a
          permutation matrix P such that JAC*P = Q*R, where JAC is the
          final calculated Jacobian, Q is orthogonal (not stored), and
          is upper triangular with diagonal elements of nonincreasing
          magnitude.  Column j of P is column IPVT(j) of the identity
          matrix.
        QTF is an output array of length N which contains the first N
          elements of the vector (Q transpose)*FVEC.
        WA1, WA2, and WA3 are work arrays of length N.
        WA4 is a work array of length M.
 
  4. Successful completion.
        The accuracy of LMDER is controlled by the convergence parame-
        ters FTOL, XTOL, and GTOL.  These parameters are used in tests
        which make three types of comparisons between the approximation
        X and a solution XSOL.  LMDER terminates when any of the tests
        is satisfied.  If any of the convergence parameters is less than
        the machine precision (as defined by the MINPACK function
        DPMPAR(1)), then LMDER only attempts to satisfy the test define
        by the machine precision.  Further progress is not usually pos-
        sible.
        The tests assume that the functions and the Jacobian are coded
        consistently, and that the functions are reasonably well
        behaved.  If these conditions are not satisfied, then LMDER may
        incorrectly indicate convergence.  The coding of the Jacobian
        can be checked by the MINPACK subroutine CHKDER.  If the Jaco-
        bian is coded correctly, then the validity of the answer can be
        checked, for example, by rerunning LMDER with tighter toler-
        ances.
        First convergence test.  If ENORM(Z) denotes the Euclidean norm
          of a vector Z, then this test attempts to guarantee that
                ENORM(FVEC) .LE. (1+FTOL)*ENORM(FVECS),
          where FVECS denotes the functions evaluated at XSOL.  If this
          condition is satisfied with FTOL = 10**(-K), then the final
          residual norm ENORM(FVEC) has K significant decimal digits an
          INFO is set to 1 (or to 3 if the second test is also satis-
          fied).  Unless high precision solutions are required, the
          recommended value for FTOL is the square root of the machine
          precision.
 
                                                                  Page
        Second convergence test.  If D is the diagonal matrix whose
          entries are defined by the array DIAG, then this test attempt
          to guarantee that
                ENORM(D*(X-XSOL)) .LE. XTOL*ENORM(D*XSOL).
          If this condition is satisfied with XTOL = 10**(-K), then the
          larger components of D*X have K significant decimal digits an
          INFO is set to 2 (or to 3 if the first test is also satis-
          fied).  There is a danger that the smaller components of D*X
          may have large relative errors, but if MODE = 1, then the
          accuracy of the components of X is usually related to their
          sensitivity.  Unless high precision solutions are required,
          the recommended value for XTOL is the square root of the
          machine precision.
        Third convergence test.  This test is satisfied when the cosine
          of the angle between FVEC and any column of the Jacobian at X
          is at most GTOL in absolute value.  There is no clear rela-
          tionship between this test and the accuracy of LMDER, and
          furthermore, the test is equally well satisfied at other crit-
          ical points, namely maximizers and saddle points.  Therefore,
          termination caused by this test (INFO = 4) should be examined
          carefully.  The recommended value for GTOL is zero.
 
  5. Unsuccessful completion.
        Unsuccessful termination of LMDER can be due to improper input
        parameters, arithmetic interrupts, or an excessive number of
        function evaluations.
        Improper input parameters.  INFO is set to 0 if N .LE. 0, or
          M .LT. N, or LDFJAC .LT. M, or FTOL .LT. 0.D0, or
          XTOL .LT. 0.D0, or GTOL .LT. 0.D0, or MAXFEV .LE. 0, or
          FACTOR .LE. 0.D0.
        Arithmetic interrupts.  If these interrupts occur in the FCN
          subroutine during an early stage of the computation, they may
          be caused by an unacceptable choice of X by LMDER.  In this
          case, it may be possible to remedy the situation by rerunning
          LMDER with a smaller value of FACTOR.
        Excessive number of function evaluations.  A reasonable value
          for MAXFEV is 100*(N+1).  If the number of calls to FCN with
          IFLAG = 1 reaches MAXFEV, then this indicates that the routine
          is converging very slowly as measured by the progress of FVEC
          and INFO is set to 5.  In this case, it may be helpful to
          restart LMDER with MODE set to 1.
 
  6. Characteristics of the algorithm.
        LMDER is a modification of the Levenberg-Marquardt algorithm.
 
                                                                  Page
        Two of its main characteristics involve the proper use of
        implicitly scaled variables (if MODE = 1) and an optimal choice
        for the correction.  The use of implicitly scaled variables
        achieves scale invariance of LMDER and limits the size of the
        correction in any direction where the functions are changing
        rapidly.  The optimal choice of the correction guarantees (under
        reasonable conditions) global convergence from starting points
        far from the solution and a fast rate of convergence for prob-
        lems with small residuals.
        Timing.  The time required by LMDER to solve a given problem
          depends on M and N, the behavior of the functions, the accu-
          racy requested, and the starting point.  The number of arith-
          metic operations needed by LMDER is about N**3 to process each
          evaluation of the functions (call to FCN with IFLAG = 1) and
          M*(N**2) to process each evaluation of the Jacobian (call to
          FCN with IFLAG = 2).  Unless FCN can be evaluated quickly, th
          timing of LMDER will be strongly influenced by the time spent
          in FCN.
        Storage.  LMDER requires M*N + 2*M + 6*N double precision sto-
          rage locations and N integer storage locations, in addition t
          the storage required by the program.  There are no internally
          declared storage arrays.
 
  7. Subprograms required.
        USER-supplied ...... FCN
        MINPACK-supplied ... DPMPAR,ENORM,LMPAR,QRFAC,QRSOLV
        FORTRAN-supplied ... DABS,DMAX1,DMIN1,DSQRT,MOD
 
  8. References.
        Jorge J. More, The Levenberg-Marquardt Algorithm, Implementation
        and Theory. Numerical Analysis, G. A. Watson, editor.
        Lecture Notes in Mathematics 630, Springer-Verlag, 1977.
 
  9. Example.
        The problem is to determine the values of x(1), x(2), and x(3)
        which provide the best fit (in the least squares sense) of
              x(1) + u(i)/(v(i)*x(2) + w(i)*x(3)),  i = 1, 15
        to the data
              y = (0.14,0.18,0.22,0.25,0.29,0.32,0.35,0.39,
                   0.37,0.58,0.73,0.96,1.34,2.10,4.39),
 
                                                                  Page
        where u(i) = i, v(i) = 16 - i, and w(i) = min(u(i),v(i)).  The
        i-th component of FVEC is thus defined by
              y(i) - (x(1) + u(i)/(v(i)*x(2) + w(i)*x(3))).
  C     **********
  C
  C     DRIVER FOR LMDER EXAMPLE.
  C     DOUBLE PRECISION VERSION
  C
  C     **********
        INTEGER J,M,N,LDFJAC,MAXFEV,MODE,NPRINT,INFO,NFEV,NJEV,NWRITE
        INTEGER IPVT(3)
        DOUBLE PRECISION FTOL,XTOL,GTOL,FACTOR,FNORM
        DOUBLE PRECISION X(3),FVEC(15),FJAC(15,3),DIAG(3),QTF(3),
       *                 WA1(3),WA2(3),WA3(3),WA4(15)
        DOUBLE PRECISION ENORM,DPMPAR
        EXTERNAL FCN
  C
  C     LOGICAL OUTPUT UNIT IS ASSUMED TO BE NUMBER 6.
  C
        DATA NWRITE /6/
  C
        M = 15
        N = 3
  C
  C     THE FOLLOWING STARTING VALUES PROVIDE A ROUGH FIT.
  C
        X(1) = 1.D0
        X(2) = 1.D0
        X(3) = 1.D0
  C
        LDFJAC = 15
  C
  C     SET FTOL AND XTOL TO THE SQUARE ROOT OF THE MACHINE PRECISION
  C     AND GTOL TO ZERO. UNLESS HIGH PRECISION SOLUTIONS ARE
  C     REQUIRED, THESE ARE THE RECOMMENDED SETTINGS.
  C
        FTOL = DSQRT(DPMPAR(1))
        XTOL = DSQRT(DPMPAR(1))
        GTOL = 0.D0
  C
        MAXFEV = 400
        MODE = 1
        FACTOR = 1.D2
        NPRINT = 0
  C
        CALL LMDER(FCN,M,N,X,FVEC,FJAC,LDFJAC,FTOL,XTOL,GTOL,
       *           MAXFEV,DIAG,MODE,FACTOR,NPRINT,INFO,NFEV,NJEV,
       *           IPVT,QTF,WA1,WA2,WA3,WA4)
        FNORM = ENORM(M,FVEC)
        WRITE (NWRITE,1000) FNORM,NFEV,NJEV,INFO,(X(J),J=1,N)
        STOP
   1000 FORMAT (5X,31H FINAL L2 NORM OF THE RESIDUALS,D15.7 //
 
                                                                  Page
       *        5X,31H NUMBER OF FUNCTION EVALUATIONS,I10 //
       *        5X,31H NUMBER OF JACOBIAN EVALUATIONS,I10 //
       *        5X,15H EXIT PARAMETER,16X,I10 //
       *        5X,27H FINAL APPROXIMATE SOLUTION // 5X,3D15.7)
  C
  C     LAST CARD OF DRIVER FOR LMDER EXAMPLE.
  C
        END
        SUBROUTINE FCN(M,N,X,FVEC,FJAC,LDFJAC,IFLAG)
        INTEGER M,N,LDFJAC,IFLAG
        DOUBLE PRECISION X(N),FVEC(M),FJAC(LDFJAC,N)
  C
  C     SUBROUTINE FCN FOR LMDER EXAMPLE.
  C
        INTEGER I
        DOUBLE PRECISION TMP1,TMP2,TMP3,TMP4
        DOUBLE PRECISION Y(15)
        DATA Y(1),Y(2),Y(3),Y(4),Y(5),Y(6),Y(7),Y(8),
       *     Y(9),Y(10),Y(11),Y(12),Y(13),Y(14),Y(15)
       *     /1.4D-1,1.8D-1,2.2D-1,2.5D-1,2.9D-1,3.2D-1,3.5D-1,3.9D-1,
       *      3.7D-1,5.8D-1,7.3D-1,9.6D-1,1.34D0,2.1D0,4.39D0/
  C
        IF (IFLAG .NE. 0) GO TO 5
  C
  C     INSERT PRINT STATEMENTS HERE WHEN NPRINT IS POSITIVE.
  C
        RETURN
      5 CONTINUE
        IF (IFLAG .EQ. 2) GO TO 20
        DO 10 I = 1, 15
           TMP1 = I
           TMP2 = 16 - I
           TMP3 = TMP1
           IF (I .GT. 8) TMP3 = TMP2
           FVEC(I) = Y(I) - (X(1) + TMP1/(X(2)*TMP2 + X(3)*TMP3))
     10    CONTINUE
        GO TO 40
     20 CONTINUE
        DO 30 I = 1, 15
           TMP1 = I
           TMP2 = 16 - I
           TMP3 = TMP1
           IF (I .GT. 8) TMP3 = TMP2
           TMP4 = (X(2)*TMP2 + X(3)*TMP3)**2
           FJAC(I,1) = -1.D0
           FJAC(I,2) = TMP1*TMP2/TMP4
           FJAC(I,3) = TMP1*TMP3/TMP4
     30    CONTINUE
     40 CONTINUE
        RETURN
  C
  C     LAST CARD OF SUBROUTINE FCN.
  C
        END
 
                                                                  Page
        Results obtained with different compilers or machines
        may be slightly different.
        FINAL L2 NORM OF THE RESIDUALS  0.9063596D-01
        NUMBER OF FUNCTION EVALUATIONS         6
        NUMBER OF JACOBIAN EVALUATIONS         5
        EXIT PARAMETER                         1
        FINAL APPROXIMATE SOLUTION
         0.8241058D-01  0.1133037D+01  0.2343695D+01